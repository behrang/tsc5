#include "imports/stdlib.fc";

() save_data(slice admin_address, cell users, int total) impure {
    begin_cell()
        .store_slice(admin_address)
        .store_dict(users)
        .store_uint(total, 32)
        .end_cell()
        .set_data();
}

(slice, cell, int) load_data() {
    slice ds = get_data().begin_parse();
    slice admin_address = ds~load_msg_addr();
    cell users = ds~load_dict();
    int total = 0;
    if ds.slice_bits() {
        total = ds~load_uint(32);
    }
    return ( admin_address, users, total );
}

() add_user(slice src, slice s) impure {
    ( slice admin_address, cell users, int total ) = load_data();

    int query_id = s~load_uint(64);
    slice address = s~load_msg_addr();
    int share = s~load_uint(32);

    throw_unless(120, equal_slice_bits(src, admin_address));
    accept_message();

    ( _, int addr ) = parse_std_addr(address);
    ( slice value, int f? ) = users.udict_get?(256, addr);
    if f? {
        total -= value~load_uint(32);
    }
    total += share;
    users~udict_set_builder(256, addr, begin_cell().store_uint(share, 32));

    save_data(admin_address, users, total);
}

() remove_user(slice src, slice s) impure {
    ( slice admin_address, cell users, int total ) = load_data();

    int query_id = s~load_uint(64);
    slice address = s~load_msg_addr();

    throw_unless(120, equal_slice_bits(src, admin_address));
    accept_message();

    ( _, int addr ) = parse_std_addr(address);
    ( slice value, int d? ) = users~udict_delete_get?(256, addr);
    throw_unless(121, d?);
    total -= value~load_uint(32);

    save_data(admin_address, users, total);
}

() split(slice src, slice s, int amount) impure {
    ( slice admin_address, cell users, int total ) = load_data();

    int query_id = s~load_uint(64);

    throw_unless(122, ~ users.null?());
    accept_message();

    ;; todo: handle large list of users
    while ~ users.null?() {
        ( int addr, slice value, _ ) = users~udict::delete_get_min(256);
        int share = value~load_uint(32);
        int x = muldiv(amount, share, total);
        cell msg = begin_cell()
            .store_uint(0x10, 6)
            .store_uint(0x400, 11)
            .store_uint(addr, 256)
            .store_coins(x)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .end_cell();
        send_raw_message(msg, 1);
    }
}

() notification(slice src, slice s) impure {
    ( slice admin_address, cell users, int total ) = load_data();

    int query_id = s~load_uint(64);
    int amount = s~load_coins();

    throw_unless(122, ~ users.null?());
    accept_message();

    ;; todo: handle large list of users
    while ~ users.null?() {
        ( int addr, slice value, _ ) = users~udict::delete_get_min(256);
        int share = value~load_uint(32);
        int x = muldiv(amount, share, total);
        cell transfer = begin_cell()
            .store_uint(0x0f8a7ea5, 32)
            .store_uint(query_id, 64)
            .store_coins(x)
            .store_uint(0x400, 11)
            .store_uint(addr, 256)
            .store_uint(0x400, 11)
            .store_uint(addr, 256)
            .store_uint(0, 1)
            .store_coins(1)
            .store_uint(0, 1)
            .end_cell();
        cell msg = begin_cell()
            .store_uint(0x10, 6)
            .store_slice(src)
            .store_coins(20000000)
            .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_ref(transfer)
            .end_cell();
        send_raw_message(msg, 1);
    }
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    slice src = cs~load_msg_addr();

    if flags & 1 {
        return ();
    }

    int op = 0;
    if in_msg_body.slice_bits() {
        op = in_msg_body~load_uint(32);
    }

    if op == 0x368ddef3 {
        return add_user(src, in_msg_body);
    }

    if op == 0x278205c8 {
        return remove_user(src, in_msg_body);
    }

    if op == 0x068530b3 {
        return split(src, in_msg_body, msg_value);
    }

    if op == 0x7362d09c {
        return notification(src, in_msg_body);
    }
}

cell get_users() method_id {
    ( _, cell users, _ ) = load_data();
    return users;
}

int get_user_share(slice address) method_id {
    ( _, cell users, _ ) = load_data();
    ( _, int addr ) = parse_std_addr(address);
    ( slice value, int f? ) = users.udict_get?(256, addr);
    return f? ? value~load_uint(32) : 0;
}
