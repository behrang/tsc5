#include "imports/stdlib.fc";

() save_data(int seqno, slice receiver, int execution_time, int public_key) impure inline {
    begin_cell()
        .store_uint(public_key, 256)
        .store_uint(execution_time, 32)
        .store_slice(receiver)
        .store_uint(seqno, 32)
        .end_cell()
        .set_data();
}

() update(slice s) impure inline {
    s~load_uint(64);
    slice signature = s~load_bits(512);
    cell ref = s~load_ref();
    slice ss = ref.begin_parse();
    int locked_for = ss~load_uint(32);
    int new_seqno = ss~load_uint(32);

    slice ds = get_data().begin_parse();
    int public_key = ds~load_uint(256);
    int execution_time = ds~load_uint(32);
    slice receiver = ds~load_msg_addr();
    int seqno = ds~load_uint(32);

    throw_unless(119, new_seqno == seqno + 1);
    throw_unless(120, check_signature(ref.cell_hash(), signature, public_key));
    throw_unless(121, locked_for);

    execution_time -= now();

    throw_unless(122, execution_time >= 0);
    throw_unless(123, locked_for >= execution_time);

    accept_message();

    save_data(new_seqno, receiver, now() + locked_for, public_key);
}

() claim() impure inline {
    slice ds = get_data().begin_parse();
    ds~skip_bits(256);
    int execution_time = ds~load_uint(32);
    slice receiver = ds~load_msg_addr();

    throw_unless(124, execution_time < now());

    accept_message();

    cell msg = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(receiver)
        .store_uint(0, 4 + 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .end_cell();
    send_raw_message(msg, 128);
}

() recv_internal(cell in_msg_full, slice in_msg_body) impure {
}

() recv_external(slice in_msg_body) impure {
    int op = in_msg_body~load_uint(32);

    if op == 0x9df10277 {
        return update(in_msg_body);
    }

    return claim();
}

int get_seqno() method_id {
    return get_data().begin_parse().skip_bits(256 + 32 + 267).preload_uint(32);
}

int get_execution_time() method_id {
    return get_data().begin_parse().skip_bits(256).preload_uint(32);
}
